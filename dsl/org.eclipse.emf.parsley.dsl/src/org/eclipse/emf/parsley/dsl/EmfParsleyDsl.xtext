grammar org.eclipse.emf.parsley.dsl.EmfParsleyDsl with
    org.eclipse.xtext.xbase.Xbase

import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types

generate model "http://www.eclipse.org/emf/parsley/dsl/EmfParsleyDsl"

Model:
	importSection=XImportSection?
	module = Module;

Module:
	'module' name=QualifiedName (extendsClause=ExtendsClause)? '{'
		(
			(labelProvider = LabelProvider)?
			&
			(featureCaptionProvider = FeatureCaptionProvider)?
			&
			(formFeatureCaptionProvider = FormFeatureCaptionProvider)?
			&
			(dialogFeatureCaptionProvider = DialogFeatureCaptionProvider)?
			&
			(featuresProvider = FeaturesProvider)?
			&
			(formControlFactory = FormControlFactory)?
			&
			(dialogControlFactory = DialogControlFactory)?
			&
			(proposalCreator = ProposalCreator)?
			&
			(viewerContentProvider = ViewerContentProvider)?
			&
			(partsSpecifications = PartsSpecifications)?
		)
	'}'
;

ExtendsClause: 'extends' superType=JvmTypeReference;

LabelProvider:
	{LabelProvider} 'labelProvider' '{'
		((
		'text' '{'
			labelSpecifications+=LabelSpecification*
		'}'
		)?
		&
		(
		'image' '{'
			imageSpecifications+=LabelSpecification*
		'}'
		)?)
	'}'
;

LabelSpecification:
	parameterType=JvmTypeReference (name=ValidID)?
	'->'
	expression = XExpression
;

FeatureCaptionProvider:
	{FeatureCaptionProvider} 'featureCaptionProvider' '{'
		((
		'text' '{'
			specifications+=FeatureCaptionSpecification*
		'}'
		)?)
	'}'
;

FormFeatureCaptionProvider:
	{FormFeatureCaptionProvider} 'formFeatureCaptionProvider' '{'
		((
		'text' '{'
			specifications+=FeatureCaptionSpecification*
		'}'
		)?
		&
		(
		'label' '{'
			labelSpecifications+=FeatureCaptionSpecification*
		'}'
		)?)
	'}'
;

DialogFeatureCaptionProvider:
	{DialogFeatureCaptionProvider} 'dialogFeatureCaptionProvider' '{'
		((
		'text' '{'
			specifications+=FeatureCaptionSpecification*
		'}'
		)?
		&
		(
		'label' '{'
			labelSpecifications+=FeatureCaptionSpecification*
		'}'
		)?)
	'}'
;

FeatureCaptionSpecification:
	parameterType=JvmTypeReference ':' feature=[types::JvmMember]
	'->'
	expression = XExpression
;

FeaturesProvider:
	{FeaturesProvider} 'featuresProvider' '{'
		((
		'features' '{'
			featureSpecifications+=FeatureSpecification*
		'}'
		)?)
	'}'
;

FeatureSpecification:
	parameterType=JvmTypeReference
	'->'
	features += [types::JvmMember] (',' features += [types::JvmMember])*
;

FormControlFactory:
	{FormControlFactory} 'formControlFactory' '{'
		(
		'control' '{'
			controlSpecifications+=ControlFactorySpecification*
		'}'
		)?
	'}'
;

DialogControlFactory:
	{DialogControlFactory} 'dialogControlFactory' '{'
		(
		'control' '{'
			controlSpecifications+=ControlFactorySpecification*
		'}'
		)?
	'}'
;

ControlFactorySpecification:
	parameterType=JvmTypeReference ':' feature=[types::JvmMember]
	'->'
	expression = XExpression
	('target' target = XExpression)?
;

ProposalCreator:
	{ProposalCreator} 'proposals' '{'
		proposalsSpecifications+=ProposalSpecification*
	'}'
;

ProposalSpecification:
	parameterType=JvmTypeReference ':' feature=[types::JvmMember]
	'->'
	expression = XExpression
;

ViewerContentProvider:
	{ViewerContentProvider} 'viewerContentProvider' '{'
		((
		'children' '{'
			childrenSpecifications+=LabelSpecification*
		'}'
		)?)
	'}'
;

// a utility base class for rules which access features of an EClass
EmfFeatureAccess:
	FeatureCaptionSpecification |
	FeatureSpecification |
	ControlFactorySpecification |
	ProposalSpecification
;

// a utility base class for rules with 'extends' clause
WithExtendsClause:
	Module
;

PartsSpecifications:
	{PartsSpecifications} 'parts' '{'
		parts += PartSpecification*
	'}'
;

PartSpecification: ViewSpecification;

ViewSpecification:
	{ViewSpecification} 'viewpart' id=QualifiedName '{'
		'viewname' viewName=STRING
		'viewclass' type=JvmTypeReference
		('viewcategory' category=QualifiedName)?
	'}'
;