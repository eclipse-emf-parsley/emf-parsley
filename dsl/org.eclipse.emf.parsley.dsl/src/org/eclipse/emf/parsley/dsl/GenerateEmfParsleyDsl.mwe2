module org.eclipse.emf.parsley.dsl.GenerateEmfParsleyDsl

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "org.eclipse.emf.parsley.dsl"
				rootPath = rootPath
				runtimeTest = {
					enabled = true
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}
		language = StandardLanguage {
			name = "org.eclipse.emf.parsley.dsl.EmfParsleyDsl"
			fileExtensions = "parsley"

			serializer = {
				generateStub = false
			}
			formatter = {
				generateStub = true
				generateXtendStub = true
			}
			newProjectWizardForEclipse = {
				generate = true
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
			}
			scopeProvider = {
				generateXtendStub = false
			}
			labelProvider = {
				generateXtendStub = false
			}
			outline = {
				generateXtendStub = false
			}
			quickFixProvider = {
				generateXtendStub = false
			}
		}
	}
}
